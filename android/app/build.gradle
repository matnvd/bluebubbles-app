def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"

android {
    namespace "com.bluebubbles.messaging"
    compileSdk 34

    lintOptions {
        checkReleaseBuilds false
        disable 'InvalidPackage'
    }

    aaptOptions {
        noCompress "tflite"
    }

    defaultConfig {
        applicationId "com.bluebubbles.messaging"
        minSdkVersion 23
        targetSdkVersion 33
        versionCode 20002000 + flutterVersionCode.toInteger()
        versionName flutterVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
        debug {

        }
    }

    flavorDimensions = ["app"]

    productFlavors {
        joel {
            dimension "app"
            resValue "string", "app_name_en", "BlueBubbles Dev (Joel)"
            resValue "color", "ic_launcher_background", "#4c49de"
            resValue "string", "file_provider", "com.bluebubbles.messaging.joel.fileprovider"
            applicationId "com.bluebubbles.messaging.joel"
        }
        tanay {
            dimension "app"
            resValue "string", "app_name_en", "BlueBubbles (Tanay Special Sauce)"
            resValue "color", "ic_launcher_background", "#4c49de"
            resValue "string", "file_provider", "com.bluebubbles.messaging.tanay.fileprovider"
            applicationId "com.bluebubbles.messaging.tanay"
        }
        alpha {
            dimension "app"
            resValue "string", "app_name_en", "BlueBubbles (Alpha)"
            resValue "color", "ic_launcher_background", "#49dbde"
            resValue "string", "file_provider", "com.bluebubbles.messaging.alpha.fileprovider"
            applicationId "com.bluebubbles.messaging.alpha"
        }
        beta {
            dimension "app"
            resValue "string", "app_name_en", "BlueBubbles (Beta)"
            resValue "color", "ic_launcher_background", "#4990de"
            resValue "string", "file_provider", "com.bluebubbles.messaging.beta.fileprovider"
            applicationId "com.bluebubbles.messaging.beta"
        }
        prod {
            getIsDefault().set(true)
            dimension "app"
            resValue "string", "app_name_en", "BlueBubbles"
            resValue "color", "ic_launcher_background", "#4990de"
            resValue "string", "file_provider", "com.bluebubbles.messaging.fileprovider"
            applicationId "com.bluebubbles.messaging"
        }
    }

    buildTypes {
        release {
            productFlavors.joel.signingConfig signingConfigs.debug
            productFlavors.tanay.signingConfig signingConfigs.debug
            productFlavors.alpha.signingConfig signingConfigs.debug
            productFlavors.beta.signingConfig signingConfigs.debug
            productFlavors.prod.signingConfig signingConfigs.debug
            minifyEnabled false
            shrinkResources false
        }
    }
    sourceSets {
        main {
            java {
                srcDirs 'src/main/java', 'src/main/java/workers'
            }
        }
    }
}

flutter {
    source '../..'
}

// For Cloud Firestore okio error
configurations.all {
    resolutionStrategy {
        force 'com.squareup.okhttp:okhttp:2.7.5'
        force 'com.squareup.okio:okio:1.17.5'
    }
}

dependencies {
    // Android native functions
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.core:core-ktx:1.12.0"
    implementation "androidx.sharetarget:sharetarget:1.2.0"
    implementation 'androidx.browser:browser:1.7.0'
    implementation 'androidx.activity:activity-ktx:1.8.2'
    implementation "androidx.work:work-runtime:2.9.0"

    // Firebase items
    implementation 'com.google.firebase:firebase-messaging:23.4.0'
    implementation 'com.google.firebase:firebase-database:20.3.0'
    implementation 'com.google.firebase:firebase-messaging-directboot:23.4.0'
    implementation 'com.google.firebase:firebase-iid:21.1.0'
    implementation 'com.google.firebase:firebase-firestore:24.10.1'

    // Kotlin Coroutines (async)
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.7.3"

    // Socket IO
    implementation ('io.socket:socket.io-client:2.0.0') {
        exclude group: 'org.json', module: 'json'
    }

    // JSON parsing
    implementation 'com.google.code.gson:gson:2.10.1'
}
